BACKUP ~adrian/backup~
//AUTHOR ~Rhaella at spellholdstudios.net~
SUPPORT ~Spellhold Studios at http://www.shsforums.net/forum/627-adrian/~

VERSION ~v6.1~

README ~adrian/readme/adrian-readme-%LANGUAGE%.html~ ~adrian/readme/adrian-readme-english.html~

ALWAYS

	ACTION_IF NOT VARIABLE_IS_SET bg2_chapter BEGIN // Always block must run only once per install!

		// Determines which game is installed and if the game is an Enhanced Edition.
		OUTER_SET is_ee = (GAME_IS ~bg2ee eet~) ? 1 : 0					// EE game
		OUTER_SET is_tob = (GAME_IS ~bgt tob tutu tutu_totsc~) ? 1 : 0	// based on vanilla ToB engine
		OUTER_SET is_bg2ee = (GAME_IS "bg2ee") ? 1 : 0					// b2gee
		OUTER_SET is_eet = (GAME_IS "eet") ? 1 : 0						// eet


		/* eet compatibility */
		ACTION_IF (is_eet) BEGIN
			OUTER_SET bg2_chapter = 12
		END ELSE BEGIN
			OUTER_SET bg2_chapter = 0
		END
		OUTER_FOR (i = 1 ; i <= 10 ; ++i) BEGIN
			OUTER_SET bg2_chapter = bg2_chapter + 1
			OUTER_SPRINT name_source ~bg2_chapter_%i%~
			OUTER_SET EVAL ~%name_source%~ = bg2_chapter
		END


		// Borrowed from Edwin Romance: convert strings to UTF-8 for BGEE/BG2EE
		// No exception, everything needs to be converted
		// setup.tra contains in game texts as well as installation texts and need to be converted
		ACTION_DEFINE_ARRAY rh#noconvert BEGIN END
		// any tra files used in LANGUAGE need to be reloaded after UTF-8 conversion
		ACTION_DEFINE_ARRAY rh#reload BEGIN setup END

		LAF HANDLE_CHARSETS
			INT_VAR
				infer_charsets = 1
			STR_VAR
				tra_path = EVAL ~%MOD_FOLDER%/translations~
				noconvert_array = rh#noconvert
				reload_array = rh#reload
		END

	END

END


AUTO_TRA ~adrian/translations/%s~

LANGUAGE ~English~
         ~english~
         ~adrian/translations/english/setup.tra~

LANGUAGE ~Francais (Traduction : Ixyon)~ 
         ~french~ 
         ~adrian/translations/french/setup.tra~

LANGUAGE ~Deutsch (Teiluebersetzung fuer SvA, von Belryll)~ 
         ~german~ 
         ~adrian/translations/german/setup.tra~


/* ================= *
 *  Adrian for BGII  *
 * ================= */
BEGIN @100									// ~Adrian is a Sorcerer~
DESIGNATED 0 
LABEL ~adrian_sorcerer~
SUBCOMPONENT @0								// ~Adrian for BGII~
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1	// ~You must have Throne of Bhaal or BG2:EE installed to install Adrian NPC mod.~

OUTER_SPRINT adrian_soa "rh#adr"
OUTER_SPRINT adrian_tob "rh#adr25"

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


BEGIN @101									// ~Change Adrian's class to Cleric/Mage~
DESIGNATED 1 
LABEL ~adrian_cleric_mage~
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1

OUTER_SPRINT adrian_soa "rh#adr1"
OUTER_SPRINT adrian_tob "rh#ad25c"

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


BEGIN @102									// ~Change Adrian's class to Mage/Thief~
DESIGNATED 2 
LABEL ~adrian_mage_thief~
SUBCOMPONENT @0
REQUIRE_PREDICATE GAME_INCLUDES ~tob~ @1

OUTER_SPRINT adrian_soa "rh#adr2"
OUTER_SPRINT adrian_tob "rh#ad25t"

INCLUDE ~%MOD_FOLDER%/lib/main_component.tpa~


/* ====================================== *
 *  Install optional soundset for Adrian  *
 * ====================================== */
BEGIN @111														// ~Phaelin's soundset~
DESIGNATED 10 
LABEL ~adrian_phaelin_soundset~
SUBCOMPONENT @110												// ~Install optional soundset for Adrian~
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~adrian.tp2~ ~0~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~1~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~2~)) @130	// ~Please install Adrian NPC for BGII main component before additional content~

LAF HANDLE_AUDIO
  STR_VAR
    audio_path = ~adrian/audio/phaelin~
    oggdec_path = ~adrian/audio~
    sox_path = ~adrian/audio~
END


BEGIN @112														// ~Tatterdemalion's soundset~
DESIGNATED 11 
LABEL ~adrian_tatterdemalion_soundset~
SUBCOMPONENT @110
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~adrian.tp2~ ~0~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~1~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~2~)) @130

LAF HANDLE_AUDIO
  STR_VAR
    audio_path = ~adrian/audio/tatterdemalion~
    oggdec_path = ~adrian/audio~
    sox_path = ~adrian/audio~
END


/* ========================= *
 *  Adrian Crossmod Content  *
 * ========================= */
BEGIN @120														// ~Crossmod Content~
DESIGNATED 20 
LABEL ~adrian_crossmod_content~
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~adrian.tp2~ ~0~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~1~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~2~)) @130	// ~Please install Adrian NPC for BGII main component before additional content~

INCLUDE ~%MOD_FOLDER%/lib/adrian_crossmod.tpa~

/* ========================= *
 *  Adrian Tolerates High Reputation  *
 * ========================= */
BEGIN @141 /* Always */

DESIGNATED 30 
LABEL ~adrian_tolerates_high_reputation-always~
SUBCOMPONENT @140 /* Adrian Tolerates High Reputation */
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~adrian.tp2~ ~0~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~1~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~2~)) @130

INCLUDE ~%MOD_FOLDER%/lib/adrian_highrep_always.tpa~

BEGIN @142 /* For Active Romance or Friendship */

DESIGNATED 31 
LABEL ~adrian_tolerates_high_reputation-active_romance~
SUBCOMPONENT @140 /* Adrian Tolerates High Reputation */
REQUIRE_PREDICATE ((MOD_IS_INSTALLED ~adrian.tp2~ ~0~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~1~)
				OR (MOD_IS_INSTALLED ~adrian.tp2~ ~2~)) @130

INCLUDE ~%MOD_FOLDER%/lib/adrian_highrep_romance.tpa~



